package PING
public
  with Software;
  with Deployment;

  with ocarina_processors_leon;

---------------
-- Processes --
---------------

process A
features
  Out_Port : out event data port Software::Simple_Type;
end A;

process implementation A.Impl
subcomponents
  Pinger        : thread Software::P.Impl;
connections
  port Pinger.Data_Source -> Out_Port;
end A.Impl;

process B
features
  In_Port  : in event data port Software::Simple_Type;
end B;

process implementation B.Impl
subcomponents
  Ping_Me        : thread Software::Q.Impl;
connections
  port In_Port -> Ping_Me.Data_Sink;
end B.Impl;

memory myram
end myram;

memory segment
end segment;

memory implementation segment.i
end segment.i;

memory implementation  myram.i
subcomponents
	segment1 : memory segment.i;
	segment2 : memory segment.i;
end myram.i;

------------
-- System --
------------

system PING
end PING;

system implementation PING.Impl
subcomponents
  Node_A : process A.Impl;
  Node_B : process B.Impl;
  mem :  memory myram.i;
  cpu : processor ocarina_processors_leon::leon3.xtratum_2partitions;
connections
  port Node_A.Out_Port -> Node_B.In_Port;
properties
  actual_processor_binding => (reference (cpu.part1)) applies to Node_A;
  actual_processor_binding => (reference (cpu.part2)) applies to Node_B;
  actual_memory_binding => (reference (mem.segment1)) applies to Node_A;
  actual_memory_binding => (reference (mem.segment2)) applies to Node_B;
end PING.Impl;

end PING;
